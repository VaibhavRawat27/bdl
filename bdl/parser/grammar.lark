start: statement+

statement: load
         | preview
         | show
         | filter
         | search
         | add_column
         | sort
         | group
         | summarize

load: "load" NAME "from" STRING
preview: "preview" NAME NUMBER?
show: "show" NAME

filter: "filter" NAME "where" condition
search: "search" NAME "for" STRING

add_column: "add" "column" NAME "=" expression
sort: "sort" NAME "by" NAME ORDER

group: "group" NAME "by" NAME
summarize: "summarize" NAME AGG NAME

condition: NAME COMP_OP value
expression: NAME ARITH_OP NAME

value: NUMBER | STRING

COMP_OP: "=" | ">" | "<" | ">=" | "<=" | "!="
ARITH_OP: "+" | "-" | "*" | "/"

ORDER: "asc" | "desc"
AGG: "total" | "average" | "min" | "max" | "count"

NAME: /[a-zA-Z_][a-zA-Z0-9_]*/
STRING: ESCAPED_STRING
NUMBER: INT

%import common.INT
%import common.ESCAPED_STRING
%import common.WS
%import common.NEWLINE

%ignore WS
%ignore NEWLINE
